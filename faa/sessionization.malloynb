>>>markdown
# Sessionization
Trace routes of lanes throug the sky.

If we think of aircraft flights as a transaction.  We can roll up all transactions in a given day by a carrier into a single nested table. Note: This represents a small sample of the complete FAA dataset!
>>>malloy
import {flights} from 'flights.malloy'
>>>markdown
## Rollup by Carrier Day.
* Compute the number of flights on that carrier and day.
* For each plane, calculate the number of flight legs on each day
* Produce a subtable of each play/day of all the flight legs and delays
>>>malloy
run: flights -> {
  where: 
    dep_time.day = @2002-03-03,
    carrier = 'WN' | 'DL'
  group_by: flight_date is dep_time.day
  group_by: carrier
  aggregate: 
    daily_flight_count is flight_count
    aircraft.aircraft_count
  nest: per_plane_data is {
    top: 20
    group_by: tail_num
    aggregate: plane_flight_count is flight_count
    nest: flight_legs is {
      order_by: 3
      calculate: flight_leg is row_number()
      group_by:
        tail_num
        dep_minute is dep_time.minute
        origin_code
        dest_code is destination_code
        dep_delay
        arr_delay
    }
  }
}